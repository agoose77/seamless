file_input: (type | NEWLINE)* ENDMARKER
type: 'Type' NAME ':' suite

suite: NEWLINE INDENT [docstring] (body_stmt|NEWLINE)+ DEDENT
docstring: LIT* NEWLINE

body_stmt: stmt | block

stmt: declaration_stmt

# declaration stmts
declaration_stmt: default_stmt | optional_stmt | default_array_stmt

# Could parse specific types here, but complicates parser
default_stmt: typed_id ['=' py_test_list] NEWLINE
default_array_stmt: typed_id array_length ['=' '[' py_test_list_comp ']'] NEWLINE
optional_stmt: '*' typed_id [array_length] NEWLINE

array_length: '[' NUMBER ']'

# type declarations
enum_id: 'Enum' NAME '(' STRING (',' STRING)+ ')'
builtin_id: ("Integer" | "Bool" | "String" | "Float") NAME
generic_id: NAME NAME

typed_id: enum_id | builtin_id | generic_id

# block stmts
block: form_block | validate_block | py_func_def

form_block: 'form' ':' block_suite
validate_block: 'validate' ':' block_suite

block_suite: py_suite | if_stmt_inline
if_stmt_inline: 'if' py_test ':'